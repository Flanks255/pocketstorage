plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.157'
    id "me.modmuss50.mod-publish-plugin" version "0.4.5"
}

repositories {
    maven { url = 'https://maven.creeperhost.net/' }
    maven { url "https://www.cursemaven.com" }
}

def ENV = System.getenv()
version = "${mod_version}+${minecraft_version}-b${ENV.GITHUB_RUN_NUMBER ?: 9999}"
group = 'com.flanks255'
base {
    archivesName = "pocketstorage"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

sourceSets.main.resources { srcDir 'src/generated/resources' }
minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
    // applies to all the run configs below
    configureEach {
        //Limit ram usage for the dev environment to 4GB
        jvmArgument '-Xmx8G'

        modSource project.sourceSets.main
    }

    client {
        systemProperty 'neoforge.enabledGameTestNamespaces', "pocketstorage"
        programArguments.addAll('--width', '1280', '--height', '720')
        if  (ENV.MCDEVUSER) {
            programArguments.addAll('--username', ENV.MCDEVUSER)
        }
        if (ENV.MCDEVUUID) {
            programArguments.addAll('--uuid', ENV.MCDEVUUID)
        }
    }
    client2 {
        configure ("client")

        programArguments.addAll '--username', 'Dev2'

        systemProperty 'neoforge.enabledGameTestNamespaces', "pocketstorage"
    }

    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', "pocketstorage"
        programArgument '--nogui'
    }

    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', "pocketstorage"
    }

    data {
        programArguments.addAll '--mod', "pocketstorage", '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}



dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"
    compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"//new
    compileOnly "mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"

    runtimeOnly "curse.maven:JADE-324717:5493270"
//
//    runtimeOnly fg.deobf("curse.maven:TOP-245211:3671753")
//    runtimeOnly fg.deobf("curse.maven:FS-556861:3820051")
//    runtimeOnly fg.deobf("curse.maven:TITANIUM-287342:3819942")
    //runtimeOnly fg.deobf("curse.maven:BUILDINGGADGETS-298187:3553977")
}

jar {
    manifest {
        attributes([
                "Specification-Title": "psu",
                "Specification-Vendor": "Flanks255",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"Flanks255",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}


publishing {
    publications {
        register('mavenJava', MavenPublication) {
            groupId project.group
            artifactId base.archivesName.get()
            version project.version
            from components.java
        }
    }

    repositories {
        if (ENV.SAPS_TOKEN) {
            maven {
                url "https://maven.saps.dev/minecraft"
                credentials {
                    username = "flanks"
                    password = "${ENV.SAPS_TOKEN}"
                }
            }
        }
    }
}

//https://modmuss50.github.io/mod-publish-plugin/
publishMods {
    dryRun = !providers.environmentVariable("CURSEFORGE_KEY").isPresent()
    file = jar.archiveFile
    changelog = file("./CHANGELOG.md").text
    modLoaders.add("NeoForge")
    type = BETA
    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_KEY")
        projectId = project.curseforge_id
        minecraftVersions.add(project.minecraft_version)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}